buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-cargo-plugin:2.6.1'
    }
}

wrapper {
    gradleVersion = '5.4.1'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    repositories {
        jcenter()
    }

    dependencies {
        compile group: 'com.sun.xml.ws', name: 'rt', version: '3.0.1'
    }
}

def webProjects() {
    subprojects.findAll { subproject -> subproject.plugins.hasPlugin('war') }
}

gradle.projectsEvaluated {
    configure(webProjects()) {
        apply plugin: 'com.bmuschko.cargo'

        cargo {
            containerId = 'glassfish4x'

            local {
                installer {
                    installUrl = 'https://download.oracle.com/glassfish/4.1.2/release/glassfish-4.1.2.zip'
                    downloadDir = file("$buildDir/download")
                    extractDir = file("$buildDir/extract")
                }
            }
        }

        cargoRunLocal.dependsOn assemble
    }
}