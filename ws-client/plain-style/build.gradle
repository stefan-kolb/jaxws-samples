apply plugin:'application'

mainClassName = "de.uniba.dsg.jaxws.DemoWSClient"

run {
    standardInput = System.in
}

configurations {
    jaxws
}

dependencies {
    jaxws group: 'com.sun.xml.ws', name: 'jaxws-tools', version: '2.3.2-1'
}

project.ext {
    destDir = file("${projectDir}/src/main/generated")
}

task wsimport {
    System.setProperty('javax.xml.accessExternalSchema', 'all')

    doLast {
        ant {
            destDir.mkdirs()

            taskdef(name:'wsimport',
                    classname:'com.sun.tools.ws.ant.WsImport',
                    classpath:configurations.jaxws.asPath)
            wsimport(keep:true,
                    xnocompile: true,
                    sourcedestdir: destDir,
                    package: 'de.uniba.dsg.jaxws',
                    wsdl: "${projectDir}/src/main/resources/historyservice.wsdl",
                    binding: "${projectDir}/src/main/resources/import-binding.xml")
        }
    }
}
clean.delete destDir
compileJava.source = ["${projectDir}/src/main/java", "${projectDir}/src/main/generated"]
compileJava.dependsOn wsimport